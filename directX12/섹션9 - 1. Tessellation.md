- Hull Shader
- Tessellator (자동, 우리가 건들이지 않음)
- Domain Shader
3단계로 이루어져 있음

정점을 추가하는 단계

Geometry Shader 단계와의 차이
- GS보다는 큰 규모에서 사용한다.
- 동적 LOD 가능하다. (렌더링 단계에서)

# LOD
level of detail
카메라에서 멀어질수록 폴리곤의 개수를 줄인다.
카메라에 멀리 있다면 폴리곤이 적은 메쉬를 사용한다.
terrain 에서 사용하기도 한다.

# Control Point 
베지어 곡선의 제어점과 비슷한 느낌
정점 생성을 위한 기준점이 된다.
Control point 자체로 정점이 되기도 한다.

# Patch
control point의 그룹

# Hull Shader

## Control Point HS
Control Point 개수 만큼 호출된다. 점단위로 실행
HS_Main
- 같은 조(Patch)의 다른 control point들도 입력으로 받는다.
- `SV_OutputControlPointID`
- `SV_PrimitiveID` : 패치가 여러개라면 id도 활용가능하다.

우리의 예제에서는 무언가 하지 않고 그냥 넘어가는 코드다.

## Constant HS
Patch 단위로 실행
어떻게 삼각형을 늘려줄 것인지 결정
- `SV_TessFactor`: 삼각형의 한 변을 몇등분 할 것인지 결정
- `SV_InsideTessFactor`: 삼각형 내부에 몇개의 점으로 모이는지 결정


# Domain Shader
Hull shader에서 만들어진 새로운 정점도 인풋으로 들어온다.
- Patch : 제어점 정보
- `SV_DomainLocation` : 새로 만들어진 정점도 포함하여 각 control point에서 거리가 얼마만큼인지 비율로 알려준다. (중앙의 점은 0.33, 0.33, 0.33)
- 

# tessellation.fx
```c
#ifndef _TESSELLATION_FX_
#define _TESSELLATION_FX_

#include "params.fx"

// --------------
// Vertex Shader
// --------------

struct VS_IN
{
    float3 pos : POSITION;
    float2 uv : TEXCOORD;
};

struct VS_OUT
{
    float3 pos : POSITION;
    float2 uv : TEXCOORD;
};

VS_OUT VS_Main(VS_IN input)
{
    VS_OUT output = input;

    return output;
}

// --------------
// Hull Shader
// --------------

struct PatchTess
{
    float edgeTess[3] : SV_TessFactor;
    float insideTess : SV_InsideTessFactor;
};

struct HS_OUT
{
    float3 pos : POSITION;
    float2 uv : TEXCOORD;
};

// Constant HS
PatchTess ConstantHS(InputPatch<VS_OUT, 3> input, int patchID : SV_PrimitiveID)
{
    PatchTess output = (PatchTess)0.f;

    output.edgeTess[0] = 1;
    output.edgeTess[1] = 2;
    output.edgeTess[2] = 3;
    output.insideTess = 1;

    return output;
}

// Control Point HS
[domain("tri")] // 패치의 종류 (tri, quad, isoline)
[partitioning("integer")] // subdivision mode (integer 소수점 무시, fractional_even, fractional_odd)
[outputtopology("triangle_cw")] // (triangle_cw, triangle_ccw, line)
[outputcontrolpoints(3)] // 하나의 입력 패치에 대해, HS가 출력할 제어점 개수
[patchconstantfunc("ConstantHS")] // ConstantHS 함수 이름
HS_OUT HS_Main(InputPatch<VS_OUT, 3> input, int vertexIdx : SV_OutputControlPointID, int patchID : SV_PrimitiveID)
{
    HS_OUT output = (HS_OUT)0.f;

    output.pos = input[vertexIdx].pos;
    output.uv = input[vertexIdx].uv;

    return output;
}

// --------------
// Domain Shader
// --------------

struct DS_OUT
{
    float4 pos : SV_Position;
    float2 uv : TEXCOORD;
};

[domain("tri")]
DS_OUT DS_Main(const OutputPatch<HS_OUT, 3> input, float3 location : SV_DomainLocation, PatchTess patch)
{
    DS_OUT output = (DS_OUT)0.f;

    float3 localPos = input[0].pos * location[0] + input[1].pos * location[1] + input[2].pos * location[2];
    float2 uv = input[0].uv * location[0] + input[1].uv * location[1] + input[2].uv * location[2];

    output.pos = mul(float4(localPos, 1.f), g_matWVP);
    output.uv = uv;

    return output;
}

// --------------
// Pixel Shader
// --------------

float4 PS_Main(DS_OUT input) : SV_Target
{
    return float4(1.f, 0.f, 0.f, 1.f);
}

#endif
```


# Shader
```cpp
void Shader::CreateGraphicsShader(const wstring& path, ShaderInfo info, ShaderArg arg)
{
	_info = info;

	CreateVertexShader(path, arg.vs, "vs_5_0");
	CreatePixelShader(path, arg.ps, "ps_5_0");

	if (arg.hs.empty() == false)
		CreateHullShader(path, arg.hs, "hs_5_0");

	if (arg.ds.empty() == false)
		CreateDomainShader(path, arg.ds, "ds_5_0");

	if (arg.gs.empty() == false)
		CreateGeometryShader(path, arg.gs, "gs_5_0");
```

# Resources
```cpp
	// Tessellation
	{
		ShaderInfo info =
		{
			SHADER_TYPE::FORWARD,
			RASTERIZER_TYPE::WIREFRAME,
			DEPTH_STENCIL_TYPE::LESS,
			BLEND_TYPE::DEFAULT,
			D3D_PRIMITIVE_TOPOLOGY_3_CONTROL_POINT_PATCHLIST
			// control point 3개로 이루어진 patch리스트 라는 듰
		};

		ShaderArg arg =
		{
			"VS_Main",
			"HS_Main",
			"DS_Main",
			"",
			"PS_Main",
		};

		shared_ptr<Shader> shader = make_shared<Shader>();
		shader->CreateGraphicsShader(L"..\\Resources\\Shader\\tessellation.fx", info, arg);
		Add<Shader>(L"Tessellation", shader);
	}
```


# SceneManager
```cpp
#pragma region Tessellation Test
	{
		shared_ptr<GameObject> gameObject = make_shared<GameObject>();
		gameObject->AddComponent(make_shared<Transform>());
		gameObject->GetTransform()->SetLocalPosition(Vec3(0, 0, 300));
		gameObject->GetTransform()->SetLocalScale(Vec3(100, 100, 100));
		gameObject->GetTransform()->SetLocalRotation(Vec3(0, 0, 0));

		shared_ptr<MeshRenderer> meshRenderer = make_shared<MeshRenderer>();
		{
			shared_ptr<Mesh> mesh = GET_SINGLE(Resources)->LoadRectangleMesh();
			meshRenderer->SetMesh(mesh);
			meshRenderer->SetMaterial(GET_SINGLE(Resources)->Get<Material>(L"Tessellation"));
		}
		gameObject->AddComponent(meshRenderer);

		scene->AddGameObject(gameObject);
	}
#pragma endregion
```